grammar it.unige.csec.Lmet with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate lmet "http://www.unige.it/csec/Lmet"

Program: 
	term=Term
;

// left associative
Term:
	TerminalTerm (=>({Application.fun=current} arg=TerminalTerm)*)
;

TerminalTerm returns Term:
	'(' Term ')' |
	Unit |
	Action |
	Variable |
	Abstraction |
	Request |
	SecFrame |
	MetFrame
;

Action:
	name=ID '(' res=ID ')'
;

Variable:
	ref=[Parameter]
;

Abstraction:
	'lambda' param=Parameter '.' term=Term
;

Unit:
	'*'
;

SecFrame:
	SecPolicy '[' sub=Term ']'
;

MetFrame:
	MetPolicy '<' sub=Term '>'
;

SecPolicy:
	'policy' name=ID
;

MetPolicy:
	'ensure' Constraint 	
;

Constraint:
	'>=' val=Float |
	'<=' val=Float
;

Float returns ecore::EFloat: INT '.' INT;

Parameter:
	name=ID (':' type=Type)?
;

Request:
	'request' name=ID ':' type=Type
;

// right associative
Type:
	TerminalType ({ArrowType.left = current} '->' right=Type)?
;

TerminalType returns Type:
	'(' Type ')' |
	BasicType |
	TypeVariable
;

BasicType:
	{UnitType} '1' 
;

TypeVariable:
	typevarName=ID
;
